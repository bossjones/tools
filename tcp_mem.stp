# show receive/send buffers (rmem, wmem)
# show incoming queue (netdev_backlog)
# sysctl net.ipv4.tcp_rmem
# sysctl net.ipv4.tcp_wmem
# @ton31337

global _rcv;
global _snd;
global _rcv_queue;

function print_head()
{
        ansi_clear_screen();
        println("Probing...Type CTRL+C to stop probing.");
        println("Buffer size\t\tCount\n");
}

probe begin { print_head(); }
probe kernel.function("tcp_v4_do_rcv"), kernel.function("tcp_v6_do_rcv")
{
        _rcv[ppfunc(), $sk->sk_rcvbuf]++;
        _snd[ppfunc(), $sk->sk_sndbuf]++;
        _rcv_queue[ppfunc(), cpu()] = $sk->sk_receive_queue->qlen;
}

probe timer.s(2)
{
        print_head();
        println("# READ");
        foreach([family, buf] in _rcv- limit 10) {
                printf("%s buffer: %d bytes (%d Kbytes), count: %d\n",
                    family,
                    buf,
                    (buf / 1024),
                    _rcv[family, buf]);
        }

        println("\n# WRITE");
        foreach([family, buf] in _snd- limit 10) {
                printf("%s buffer: %d bytes (%d Kbytes), count: %d\n",
                    family,
                    buf,
                    (buf / 1024),
                    _snd[family, buf]);
        }

        println("\n# RECEIVE QUEUES");
        foreach([family, cpu] in _rcv_queue- limit 10) {
                printf("%s qlen: %d, cpu: %d\n", family, _rcv_queue[family, cpu], cpu);
        }

        delete _rcv;
        delete _snd;
}
