# show receive/send buffers (rmem, wmem)
# show incoming queue (netdev_backlog)
# sysctl net.ipv4.tcp_rmem
# sysctl net.ipv4.tcp_wmem
# @ton31337

global _rcv;
global _snd;
global _rcv_queue;

function print_head()
{
        ansi_clear_screen();
        println("Probing...Type CTRL+C to stop probing.");
        println("Buffer size\t\tCount\n");
}

probe begin { print_head(); }
probe kernel.function("tcp_v4_do_rcv")
{
        _rcv[$sk->sk_rcvbuf]++;
        _snd[$sk->sk_sndbuf]++;
        _rcv_queue[cpu()] = $sk->sk_receive_queue->qlen;
}

probe timer.s(2)
{
        print_head();
        println("# READ");
        foreach(buf in _rcv- limit 5) {
                printf("%d bytes\t\t\t%d\n", buf, _rcv[buf]);
        }

        println("\n# WRITE");
        foreach(buf in _snd- limit 5) {
                printf("%d bytes\t\t\t%d\n", buf, _snd[buf]);
        }

        println("\n# BACKLOG SIZE\t\tCPU");
        foreach(cpu in _rcv_queue- limit 10) {
                printf("%d\t\t\t%d\n", _rcv_queue[cpu], cpu);
        }

        delete _rcv;
        delete _snd;
}
